buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30"
        classpath "org.jetbrains.kotlin:kotlin-allopen:1.3.21"
    }

    ext {
        bouncyCastleVersion = "1.46"
        svnkitVersion = "1.9.3"
        commonExecVersion = "1.3"
        jacksonVersion = "2.9.2"
        jacksonDatabindVersion = "2.13.4.2"
        compressVersion = "1.15"
        javaPluginSdkVersion = "1.1.7"
        kotlinVersion = "1.5.30"
        reflectionsVersion = "0.9.10"
        injectVersion = "5.1.0"
    }
}


static def getValue(String key, Object defaultValue) {
    def value = System.getProperty(key)
    if (value == null) {
        value = System.getenv(key)
    }
    if (value == null) {
        value = defaultValue.toString()
    }
    return value
}

def mavenCredUserName = getValue("mavenCredUserName", getProperty("MAVEN_CRED_USERNAME"))
def mavenCredPassword = getValue("mavenCredPassword", getProperty("MAVEN_CRED_PASSWORD"))

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}


allprojects {
    group 'com.tencent.bk.devops'
    apply plugin: "kotlin"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "application"

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    dependencies {
        implementation "com.tencent.devops.ci-plugins:java-plugin-sdk:${javaPluginSdkVersion}"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
//        ktlint "com.github.shyiko:ktlint:0.34.2"
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.1")
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.1")
        implementation "org.bouncycastle:bcprov-jdk16:$bouncyCastleVersion"
        implementation "org.apache.commons:commons-exec:$commonExecVersion"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
        implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
        implementation "org.apache.commons:commons-compress:$compressVersion"
        implementation "org.hashids:hashids:1.0.3"
        implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
        implementation "org.tukaani:xz:1.2"
        testImplementation group: 'junit', name: 'junit', version: '4.12'
        implementation "org.reflections:reflections:$reflectionsVersion"
        implementation "org.json:json:20171018"
        implementation 'com.perforce:p4java:2021.2.2240592'
        implementation "org.apache.httpcomponents:fluent-hc:4.5.13"
        implementation "net.lingala.zip4j:zip4j:2.11.4"
        implementation "com.google.inject:guice:$injectVersion"
    }


    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        // baseName为插件默认打包名+".jar"，bkdevops-plugin.jar
        // 如果修改，则要一同修改插件task.json中的target启动命令
        // 为了省事，建议不用修改
        baseName = "CodeCCCheckAtom"
        classifier = null
        zip64 true
    }

    task copyFrontend(type:Copy) {
        from "../../frontend/dist"
        into "../../frontend/target/frontend"
    }

    task cleanTarget(type:Delete) {
        delete "../../../target"
    }

    task copyDocs(type:Copy) {
        from "../../../docs"
        into "../../../target/file/docs"
    }

    task copyImages(type:Copy) {
        from "../../../images"
        into "../../../target/file/images"
    }

    task buildZipCn(type: Zip, dependsOn: [shadowJar, copyFrontend, cleanTarget, copyDocs, copyImages]) {
        from "build/libs", "task.json", "quality.json", "../../frontend/target", "../../../target"
        into "CodeCCCheckAtom"
        archiveName "CodeCCCheckAtom.zip"
    }

    task renameEnTaskJson(type:Copy) {
        from "task_en.json"
        into "../../../target/"
        rename { String fileName ->
            // a simple way is to remove the "-$version" from the jar filename
            // but you can customize the filename replacement rule as you wish.
            fileName = "task.json"
        }
    }

    task buildZipWithEn(type: Zip, dependsOn: [buildZipCn, renameEnTaskJson]) {
        from "build/libs", "quality.json", "../../frontend/target", "../../../target"
        into "CodeCCCheckAtom"
        archiveName "CodeCCCheckAtom_en.zip"
    }

    task buildAndCopyZip(type: Copy, dependsOn: [buildZipWithEn]) {

    }

//    check.dependsOn ktlint

    // 固定入口类 不要改
    mainClassName = "com.tencent.bk.devops.atom.AtomRunner"
}